/*
 * Adapted from The MIT License (MIT)
 *
 * Copyright (c) 2018-2020 DaPorkchop_
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation
 * files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy,
 * modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software
 * is furnished to do so, subject to the following conditions:
 *
 * Any persons and/or organizations using this software must include the above copyright notice and this permission notice,
 * provide sufficient credit to the original authors of the project (IE: DaPorkchop_), as well as provide a link to the original project.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
 * OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS
 * BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

allprojects {
    ext {
        //general things
        javaVersion = "1.8"
        mapdlVersion = "0.0.1"

        //minecraft things
        mcVersion = "1.12.2"
        forgeVersion = "14.23.5.2836"
        mcpVersion = "stable_39"

        //dependency things
        gsonVersion = "2.8.5"
        guavaVersion = "28.0-jre"
        junitVersion = "4.12"
        leveldbVersion = "1.8"
        lombokVersion = "1.16.20"
        porklibVersion = "0.5.0-SNAPSHOT"
    }

    group "net.daporkchop.mapdl"
    version "$mapdlVersion"
}

subprojects {
    apply plugin: "java"

    compileJava {
        sourceCompatibility = targetCompatibility = "$javaVersion"
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    configurations {
        shade
        compile.extendsFrom shade
    }

    sourceCompatibility = 1.8
    archivesBaseName = project.name

    repositories {
        mavenLocal()
        maven {
            name = "DaPorkchop_"
            url = "https://maven.daporkchop.net/"
        }
        mavenCentral()
    }

    dependencies {
        compile ("net.daporkchop.lib:hash:$porklibVersion") {
            exclude group: "io.netty"
        }
        compile ("net.daporkchop.lib:http:$porklibVersion") {
            exclude group: "io.netty"
        }

        testCompile "junit:junit:$junitVersion"

        compileOnly "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    }

    jar {
        from {
            configurations.shade.collect {
                it.isDirectory() ? it : zipTree(it)
            }.unique()
        } {
            exclude "META-INF/**"
            exclude sourceSets.main.output.asFileTree.collect {
                return it.getPath().split("java/main/").last()
            }
        }
    }
}
